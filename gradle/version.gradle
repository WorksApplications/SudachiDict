buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar.grgit:grgit-core:5.2.0'
    }
}

String commitFromGit() {
    def git = org.ajoberstar.grgit.Grgit.open {
        dir = file('.')
    }
    def describedCommit = git.describe().toString().trim().replaceFirst('\\Av', '')
    String suffix1 = ""
    if (describedCommit.matches(".*-[0-9]+-g[0-9a-f]{7}")) {
        suffix1 = "-SNAPSHOT"
    }
    String suffix2 = ""
    if (!git.status().isClean()) {
        suffix2 = "+dirty"
    }
    return describedCommit + suffix1 + suffix2
}

String versionString() {
    if (property("dict.release") == "true") {
        return property("dict.version")
    } else {
        return commitFromGit()
    }
}

version = versionString()

tasks.register("showVersion") {
    doLast {
        println version
    }
}